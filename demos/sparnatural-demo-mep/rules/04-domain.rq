PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
CONSTRUCT {
            # set property as an autocomplete property by default
            ?p rdfs:subPropertyOf <http://data.sparna.fr/ontologies/sparnatural-config-core#AutocompleteProperty> .
            ?p rdfs:domain ?class .
            ?p rdfs:range ?classO .
            ?p rdfs:label ?propertyLabel .
        }
        where {
            ?s rdf:type ?class .
            ?s ?p ?o .
            FILTER(?class != rdf:List &&
                ?class != rdf:Property &&
                ?class != rdfs:Class &&
                ?class != owl:SymmetricProperty &&
                ?class != owl:TransitiveProperty &&
                ?class != rdfs:Datatype &&
                ?class != rdfs:ContainerMembershipProperty)
            # only properties that connect 2 entities
            FILTER(isIRI(?o))
            # fetch type of value
            ?o rdf:type ?classO .
            # fetch label of property
            OPTIONAL { ?p rdfs:label ?propertyLabel }
        }